// comment!
tokenizer {
    @skip Whitespace r'\s+'
    @skip Comment r'//.*$'
    @error ErrorToken r'\S+'
    String {
        let mut raw = false;
        if l.peek().unwrap() == b'r' {
            raw = true;
            l.next();
        }

        let mut balance = 0;
        loop {
            match l.next() {
                Some(b'#') => balance += 1,
                Some(b'\'') => break,
                _ => return None,
            }
        }

        loop {
            match l.next() {
                None => return None,
                Some(b'\\') if !raw => {
                    l.next();
                }
                Some(b'\'') => {
                    let mut balance = balance;
                    loop {
                        if balance == 0 {
                            return Literal;
                        }
                        if let Some(b'#') = l.next() {
                            balance -= 1;
                        } else {
                            break;
                        }
                    }
                }
                _ => {}
            }
        }
    }
    InlineKeyword 'inline'
    TokenizerKeyword 'tokenizer'
    RuleKeyword 'rule'
    GroupKeyword 'group'
    Ident r'\w+'
    Number '[0-9]+'
    LCurly '{'
    RCurly '}'
    LParen '('
    RParen ')'
    LBracket '['
    RBracket ']'
    LAngle '<'
    RAngle '>'
    Question '?'
    Pipe '|'
    Plus '+'
    At '@'
    Colon ':'
    Comma ','
}

rule File {
    Item*
}

rule Item {
    Tokenizer | SynRule | Group
}

rule AttributeExpr {
    Ident ( '(' String | Ident | Number ')' )?
}

rule Attribute {
    '@' Ident ( '(' <separated_list AttributeExpr ','> ')' )?
}

rule Tokenizer {
    'tokenizer' '{' TokenRule* '}'
}

inline rule delimited(bra, ket, inside) {
    bra (inside | <not ket>)* ket
}

rule ParenDelimited {
    <delimited '(' ')' DelimitedExpr>
}

rule BraceDelimited {
    <delimited '{' '}' DelimitedExpr>
}

rule BracketDelimited {
    <delimited '[' ']' DelimitedExpr>
}

inline rule DelimitedExpr {
    ParenDelimited | BraceDelimited | BracketDelimited
}

rule TokenRule {
    Attribute* Ident (String | BraceDelimited)
}

rule SynRule {
    Attribute* 'inline'? 'rule' Ident Parameters? '{' Expr '}'
}

rule Parameters {
    '(' <separated_list Ident ','> ')'
}

@pratt
rule Expr {
    Ident | String | CallExpr | ParenExpr | PreExpr | PostExpr | PostName | SeqExpr | BinExpr
}

rule CallExpr {
    '<' Ident Expr? '>'
}

rule ParenExpr {
    '(' Expr ')'
}

rule PreExpr {
    Attribute+ Expr
}

rule PostExpr {
    Expr ('?' | '+')
}

rule PostName {
    Expr ':' Ident
}

rule SeqExpr {
    Expr Expr+
}

rule BinExpr {
    Expr '|' Expr
}

rule Group {
    'group' Ident '{'
        GroupItem*
    '}' 
}

rule GroupItem {
    Ident Ident
}